package shipDock.framework.core.action
{
	
	import flash.utils.getQualifiedClassName;
	import shipDock.framework.core.interfaces.IObserver;
	import shipDock.framework.core.notice.Notice;
	
	import shipDock.framework.core.command.CommandController;
	import shipDock.framework.core.interfaces.IAction;
	import shipDock.framework.core.interfaces.IDataProxy;
	import shipDock.framework.core.interfaces.IDispose;
	import shipDock.framework.core.interfaces.INotice;
	import shipDock.framework.core.interfaces.ISubject;
	import shipDock.framework.core.notice.CallProxyedNotice;
	import shipDock.framework.core.notice.NoticeController;
	import shipDock.framework.core.notice.SetDataProxyNotifyNotice;
	import shipDock.framework.core.observer.DataProxy;
	
	/**
	 * 逻辑代理脚本类
	 *
	 * 统一管理逻辑
	 *
	 * @author shaoxin.ji
	 *
	 */
	public class Action implements IAction, IDispose
	{
		
		private static var defaultActionTarget:Action; //此默认代理对象只会被设置一次
		
		/**
		 * 获取到此逻辑代理对象时表示逻辑本体已过期
		 *
		 * @return
		 */
		static public function get defaultAction():Action
		{
			(defaultActionTarget == null) && (defaultActionTarget = new Action()); //一个没有挂载任何命令的逻辑代理对象
			return defaultActionTarget;
		}
		
		static public function set defaultAction(value:Action):void {
			(defaultActionTarget == null) && (defaultActionTarget = value);
		}
		
		/*被代理对象*/
		protected var _proxyed:*;
		/*主题对象映射*/
		protected var _subjects:Object;
		/*消息控制器*/
		protected var _noticeController:NoticeController;
		/*命令控制器*/
		protected var _commandController:CommandController;
		/*逻辑代理控制器*/
		protected var _actionCotroller:ActionController;
		/*是否被初始化*/
		protected var _isInit:Boolean;
		
		/*逻辑代理名*/
		private var _actionName:String;
		/*消息侦听集合*/
		private var _commandListeners:Object;
		/*触发被代理对象逻辑调用的消息缓存*/
		private var _callProxyedNotice:CallProxyedNotice;
		
		public function Action(name:String = null)
		{
			this._actionName = name;
			this._subjects = {};
			this._commandListeners = {};
			this._actionCotroller = ActionController.getInstance();
			this._commandController = CommandController.getInstance();
			this._noticeController = NoticeController.getInstance();
			
		}
		
		/**
		 * 销毁脚本
		 *
		 */
		public function dispose():void
		{
			
			this._commandController.removeAction(this);
			this._commandListeners = null;
			this._commandController = null;
			this._proxyed = null;
		}
		
		/**
		 * 设置数据代理的绑定
		 * 
		 * @param	dataProxyName 数据代理名
		 * @param	isUnbind 是否为取消绑定操作
		 */
		protected function bindDataProxy(dataProxyName:String):void {
			var dataProxy:IDataProxy = this.getDataProxy(dataProxyName);
			(dataProxy) && dataProxy.registered(this);
		}
		
		/**
		 * 取消数据代理的绑定
		 * 
		 * @param	dataProxyName 数据代理名
		 * @param	isUnbind 是否为取消绑定操作
		 */
		protected function unbindDataProxy(dataProxyName:String):void {
			var dataProxy:IDataProxy = this.getDataProxy(dataProxyName);
			(dataProxy) && dataProxy.unsubscribe(this);
		}
		
		/**
		 * 调用被代理对象的开放接口
		 *
		 * @param	name
		 * @param	data
		 * @param	isNewNotice
		 * @return
		 */
		public function callProxyed(name:String, data:* = null, isNewNotice:Boolean = false):*
		{
			var result:*;
			if (isNewNotice)
			{
				var notice:CallProxyedNotice = new CallProxyedNotice(name, data);
				result = this.sendNotice(notice);
			}
			else
			{
				(this._callProxyedNotice == null) && (this._callProxyedNotice = new CallProxyedNotice(name, data));//创建消息缓存
				this._callProxyedNotice.changeName(name);
				this._callProxyedNotice.changeData(data);
				result = this.sendNotice(this._callProxyedNotice);
			}
			return result;
		}
		
		/**
		 * 更新观察者
		 * 
		 * @param	notice
		 * @return
		 */
		public function notify(notice:INotice):*
		{
			return null;
		}
		
		/**
		 * 发送消息
		 *
		 * @param	notice
		 * @return
		 */
		public function sendNotice(target:*, body:* = null, subCommand:String = null, observer:IObserver = null, autoDispose:Boolean = true):*
		{
			var result:*;
			var notice:INotice;
			if(target is INotice) {
				notice = target;
			}else if (target is String) {
				notice = new Notice(target, body, observer, autoDispose);
			}
			result = this._noticeController.sendNotice(notice);
			return result;
		}
		
		public function addNotice(noticeName:String, handler:Function):void
		{
			this._noticeController.addNotice(noticeName, this, handler);
		}
		
		public function removeNotice(noticeName:String, handler:Function):void
		{
			this._noticeController.removeNotice(noticeName, this, handler);
		}
		
		/**
		 * 设置主题
		 *
		 * @param	subject
		 */
		public function setSubject(subject:ISubject):void
		{
			if (this._subjects == null)
				return;
			this._subjects[subject.subjectName] = subject;
		}
		
		/**
		 * 移除主题
		 *
		 * @param	subject
		 */
		public function removeSubject(subject:ISubject):void
		{
			if (this._subjects == null)
				return;
			delete this._subjects[subject.subjectName];
		}
		
		/**
		 * 设置命令
		 *
		 * 子类覆盖此方法，添加命令注册代码
		 *
		 */
		protected function setCommand():void
		{
		}
		
		public function initCommand(notice:INotice):void 
		{
			this.setCommand();
		}
		
		/**
		 * 设置逻辑主体，可以是界面或其他被代理的对象
		 *
		 * @param	target
		 */
		public function setProxyed(target:*):void
		{
			if (this._isInit)
				this.dispose(); //重复调用此方法是执行这里
			else
				this._isInit = true;
			this._proxyed = target;
			this.setCommand();
		}
		
		/**
		 * 获取逻辑主体的一个方法
		 *
		 * @param	methodName
		 * @return
		 */
		public function getProxyedMethod(methodName:String):Function
		{
			if (this.proxyed == null)
				return null;
			return this.proxyed[methodName];
		}
		
		/**
		 * 获取一个数据代理
		 * 
		 * @param	proxyName
		 * @return
		 */
		protected function getDataProxy(proxyName:String):IDataProxy {
			return DataProxy.getDataProxy(proxyName);
		}
		
		/**
		 * 注册命令
		 *
		 * @param command
		 *
		 */
		public function registered(noticeName:String, commandClass:Class):void
		{
			if (commandClass is Class)
			{
				this._commandListeners[noticeName] = commandClass;
				this._commandController.addCommand(this, noticeName, commandClass);
			}
		}
		
		/**
		 * 注销单个命令
		 *
		 * @param commandName
		 *
		 */
		public function unsubscribe(commandName:String):void
		{
			this._commandController.removeCommand(this, commandName);
			delete this._commandListeners[commandName];
		}
		
		/**
		 * 获取一个逻辑代理对象
		 *
		 * @param	actionName
		 * @return
		 */
		public function getAction(actionName:String):IAction
		{
			var result:IAction;
			(!!this._actionCotroller) && (result = this._actionCotroller.getAction(actionName));
			(result == null) && (result = Action.defaultAction);
			return result;
		}
		
		/**
		 * getter 逻辑代理名
		 *
		 * @return
		 *
		 */
		public function get actionName():String
		{
			var result:String = this._actionName;
			if (result == null)
			{
				result = getQualifiedClassName(this);//逻辑代理名被设置空值时默认使用类名作逻辑代理名
				result = result.split("::")[1];
				this._actionName = result;
			}
			return result;
		}
		
		/**
		 * getter 逻辑主体
		 *
		 * @return
		 *
		 */
		public function get proxyed():*
		{
			return this._proxyed;
		}
		
	}
}